     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      INDARA
     A                                      CA03
     A                                      CA12
     A                                      CA21
     A          R WINGSFL                   SFL
     A            SFLRCD         4S 0H
     A            WFFLDI        10A  O  7  2
     A  20                                  DSPATR(PC)
     A            WFFLDT         1A  O  7 15
     A            WFFLDB         5Y 0O  7 21EDTCDE(3)
     A            WFFLDP         2A  O  7 27
     A            WFFOBO         5Y 0O  7 33EDTCDE(3)
     A            WFPOSF         5Y 0O  7 39EDTCDE(3)
     A            WFNAME        10A  O  7 47COLOR(WHT)
     A            WFFTXT        50A  O  7 63
     A                                  8  6'Entêtes >'
     A                                      COLOR(PNK)
     A                                  8 16'Colonne 1 :'
     A                                      DSPATR(HI)
     A            WFCHD1        20A  O  8 28
     A                                  8 49'Colonne 2 :'
     A                                      DSPATR(HI)
     A            WFCHD2        20A  O  8 61
     A                                  8 82'Colonne 3 :'
     A                                      DSPATR(HI)
     A            WFCHD3        20A  O  8 94
     A          R WINGCTL                   SFLCTL(WINGSFL)
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      SFLMODE(&WFMODE)
     A  31                                  SFLDSP
     A  30                                  SFLDSPCTL
     A N30                                  SFLCLR
     A  30                                  SFLEND(*MORE)
     A  11                                  SFLDROP(CF11)
     A N11                                  SFLFOLD(CF11)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A            WFRRN          4S 0H      SFLRCDNBR(CURSOR)
     A            WFMODE         1A  H
     A                                  1  2USER
     A                                      COLOR(BLU)
     A                                  1 36'WingFile - Affichage de la descrip-
     A                                      tion dätailläe d''un fichier'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1115TIME
     A                                      COLOR(BLU)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WFFILE        40A  O  2 47DSPATR(RI)
     A                                  2124SYSNAME
     A                                      COLOR(BLU)
     A            WFTXT         50A  O  3 42
     A                                  4  2'Rechercher :'
     A            WFPOSITION    10A  B  4 15
     A                                  6  1' Nom zone   Type      Taille    Dä-
     A                                      but   Fin   Nom format      Descrip-
     A                                      tion                               -
     A                                                                  '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A          R WINGFOOT
     A                                 25  1'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                  '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 26  8'F3=Quitter'
     A                                      COLOR(BLU)
     A                                 26 25'F11=Informations COLHDG'
     A                                      COLOR(BLU)
     A                                 26 58'F12=Retour'
     A                                      COLOR(BLU)
     A                                 26 84'F21=Imprimer'
     A                                      COLOR(BLU)
